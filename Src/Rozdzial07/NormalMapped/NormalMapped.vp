// Shader œwiat³a normalnego mapowania
// Shader wierzcho³ków
// Richard S. Wright Jr.
// OpenGL. Ksiêga eksperta
#version 130

// Dane wejœciowe wierzcho³ków...po³o¿enie i normalna
in vec4 vVertex;
in vec3 vNormal;
in vec4 vTexture0;

uniform mat4   mvpMatrix;
uniform mat4   mvMatrix;
uniform mat3   normalMatrix;
uniform vec3   vLightPosition;

// Kolor do shadera fragmentów
vec3 vEyeNormal;
smooth out vec3 vVaryingLightDir;
smooth out vec2 vTexCoords;

vec3 vTangent = vec3(1.0, 0.0, 0.0);

void main(void) 
    { 
    vec3 b, t, v;
    
    // Obliczenie normalnej do powierzchni we wspó³rzêdnych oka
    vEyeNormal = normalMatrix * vNormal;

    // Obliczenie po³o¿enia wierzcho³ka w we wspó³rzêdnych oka
    vec4 vPosition4 = mvMatrix * vVertex;
    vec3 vPosition3 = vPosition4.xyz / vPosition4.w;

    // Obliczenie wektora do Ÿród³a œwiat³a
    vVaryingLightDir = normalize(vLightPosition - vPosition3);

    // Budowanie bazy Tangent
    t = normalize(normalMatrix * vTangent);
    b = cross(vEyeNormal, t);
    v.x = dot(vVaryingLightDir, t);
    v.y = dot(vVaryingLightDir, b);
    v.z = dot(vVaryingLightDir, vEyeNormal);
    
    vVaryingLightDir = normalize(v);

    // Przekazanie wspó³rzêdnych tekstury
    vTexCoords = vTexture0.st;

    // Przekszta³cenie geometrii
    gl_Position = mvpMatrix * vVertex;
    }
